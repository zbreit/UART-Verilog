`timescale 1ns / 1ps

/**
 * This is a UART receiver circuit that takes in serial data
 * sent by a UART transmitter (Tx) and outputs that data in a parallel
 * format. If any control or parity bits in the transmission are missing, 
 * the circuit will display an error.
 */
 
module Rx
#(
    parameter 
        WIDTH = 4'd8,          // Number of data bits
        NUM_START_BITS = 4'd1, // Number of start bits
        NUM_STOP_BITS = 4'd2,  // Number of start bits
        COUNTER_WIDTH = 4      // Size of the counter (big enough to store WIDTH + NUM_START_BITS + NUM_STOP_BITS)
)
(
    input clk,                  // System clock
    input reset,                // Whether to reset the Rx module
    input Din,                  // Serial input data (generated by Tx)
    input useOddParity,         // Whether the parity should be even or odd (1 = odd, 0 = even)
    output[WIDTH-1:0] Dout,     // Output message
    output busy,                // Whether the module is currently reading a transmission
    output receivedNewMsg,      // Whether a new message was received from Tx
    output error                // Whether there was a transmission error (i.e., parity mismatch)
);
// Derived parameter values
localparam 
    TOTAL_SIZE = NUM_START_BITS + NUM_STOP_BITS + WIDTH + 1; // Add 1 for parity bit

// Create intermediate inputs/outputs for the FSM
wire 
    nextBit,
    readAllData,
    resetTimer, 
    resetCounter, 
    readBit, 
    parityMatch,
    increment,
    outputBitForShifter,
    outputBitForBuffer,
    publishData;

// Raw output from the shift register
wire[TOTAL_SIZE-1:0] shiftData;

// Extract all of the data bits from the register
wire[WIDTH-1:0] outputData = 
    shiftData[(WIDTH + NUM_START_BITS - 1):NUM_START_BITS];

// Check that the parity from the transmission matches the expected value
wire expectedParity = shiftData[TOTAL_SIZE - NUM_STOP_BITS - 1];
wire observedParity = (^ outputData) ^ (useOddParity);
assign parityMatch = expectedParity == observedParity;

// Load data into the shift register
ShiftRegister  #(.WIDTH(TOTAL_SIZE)) shiftReg (
    .clk(clk),
    .reset(reset),
    .serialIn(Din),
    .shift(readBit),
    .allData(shiftData),
    .load(1'b0),                       // Unused by Rx
    .Din({TOTAL_SIZE{1'b0}}),          // Unused by Rx
    .outputBit(outputBitForShifter)    // Unused by Rx
);

// Instantiate the 300 Hz timer for the Baud rate
SlowerClock slowClk (
    .systemClk(clk),
    .reset(resetTimer),
    .slowerClk(nextBit)
);

// Create a counter for the number of bits that have been transmitted
ModNCounter #(.N(TOTAL_SIZE), .WIDTH(COUNTER_WIDTH)) dataCounter (
    .clk(clk),
    .increment(increment),
    .reset(resetCounter),
    .didReachN(readAllData)
);

// Create the controller
RxController receiverFSM (
    .clk(clk),
    .reset(reset),
    .Din(Din),
    .nextBit(nextBit),
    .readAllData(readAllData),
    .parityMatch(parityMatch),
    .busy(busy),
    .resetCounter(resetCounter),
    .resetTimer(resetTimer),
    .readBit(readBit),
    .increment(increment),
    .error(error),
    .receivedNewMsg(receivedNewMsg),
    .publishData(publishData)
);

// Create a register which will only update Dout when the 
// output data is ready to be published
ShiftRegister #(.WIDTH(WIDTH)) outputBuffer (
    .clk(clk),
    .reset(reset),
    .serialIn(1'b0),
    .load(publishData),
    .Din(outputData),
    .allData(Dout),
    .outputBit(outputBitForBuffer),    // Unsued by buffer
    .shift(1'b0)                       // Unused by buffer
);

endmodule
